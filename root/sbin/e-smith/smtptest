#!/usr/bin/perl

#
# smtptest -- SMTP client for nethserver-mail-common testing 
#

package NethServer::smtptest;

use strict;
use Getopt::Long; 
use Net::SMTP;
use Net::Cmd qw(CMD_OK);
use Data::Dumper;
use Carp;
use POSIX qw(strftime locale_h);


my $timestamp = strftime("%a, %d %b %Y %H:%M:%S %z", localtime());

my %options = (
    'input' => '-',
    'port' => '25',
    'host' => '127.0.0.1',
    'from' => 'unspecified@example.com',
    'subject' => 'Test message',
    'addr' => '127.0.0.1',
    'hello' => 'localhost.localdomain',
    'to' => 'postmaster@localhost',
);

GetOptions(\%options, 
	   'from=s',
	   'to=s',
	   'hello|ehlo=s',
	   'addr=s',
	   'subject=s',
	   'host|server=s',
	   'port=s',
	   'input|in=s',	  
    );

my $body = <<MESSAGE;
This is the body of the message "$options{subject}" sent on $timestamp

--
This message has been generated by smtptest script
Copyright (C) 2012 - Nethesis srl
http://www.nethesis.it
MESSAGE

my $response;

my $conn = Net::SMTP->new(Host => $options{host} . ':' . $options{port}, 
			  Hello => $options{hello}) or die();

#print "Connected to " . $conn->domain() . "\n      " . $conn->banner() . "\n     " . $conn->message() . "\n";


$conn->command(sprintf("XCLIENT NAME=%s ADDR=%s", $options{hello}, $options{addr}))->response() == CMD_OK or die($conn->message());

$conn->command(sprintf("EHLO %s", $options{hello}))->response() == CMD_OK or die($conn->message());

$conn->mail($options{from}) or die($conn->message());

$conn->recipient($options{to}) or die($conn->message());


$conn->data(
    sprintf("Subject: %s\r\n", $options{subject}),
    sprintf("Date: %s\r\n", $timestamp),
    sprintf("From: %s\r\n", $options{from}),
    "\r\n",
    $body
    ) or die($conn->message());

$conn->quit() or die($conn->message());

